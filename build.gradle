plugins {
    id "fabric-loom" version "${loom_version}"
}

version = "${project.mod_version}+${project.minecraft_version}"
group = project.maven_group

base {
    archivesName = project.mod_id
}

repositories {
    maven {
        url = "https://maven.terraformersmc.com/releases/"
    }
}

loom {
    accessWidenerPath = file("src/main/resources/${mod_id}.accesswidener")
}

loom.runs.configureEach {
    if (it.environment == "server") {
        ideConfigGenerated = false
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modApi "com.terraformersmc:modmenu:${project.mod_menu_version}"
}

processResources {
    inputs.property "version", project.version
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    from("LICENSE")
}
